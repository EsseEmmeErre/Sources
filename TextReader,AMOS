'-> Text Reader by Stefano Maria Regattin
'i> 16 novembre 2011 
'm> 17,18,19,20,23,24,25,26,27,28,29 novembre 2011 
'm> 2,8 dicembre 2011
'--------------------- 
Global ALTARISOLUZIONE
Global ALTEZZASCHERMO
Global ARCOBALENO
Global BANCOMUSICA
Global C0LOREARCOBALENO
Global FINEDELPROGRAMMA
Global LINEA
Global LINEAINCOMPLETAAFONDOSCHERMO
Global LINEEDITESTO
Global LUNGHEZZAARCOBALENO
Global LUNGHEZZALINEA
Global POSIZIONEARCOBALENO
Global SCHERMO
Global SUONA
Global TESTO
Global TESTOGIUNTOAFONDOSCHERMO
Global TIPOARCOBALENO
Global XSCHERMO
Global YSCHERMO

Global FINEDELLAPAGINAPREMIUNTASTOPERCONTINUARE$
Global FINEDELTESTOPREMIUNTASTOPERUSCIRE$
Global LINEADITESTO$
Global NOMEPROGRAMMA$
Global TASTIDAPREMERE$
Global TASTOFUGA$

FINEDELLAPAGINAPREMIUNTASTOPERCONTINUARE$="PRESS A KEY TO CONTINUE..."
FINEDELTESTOPREMIUNTASTOPERUSCIRE$="THE TEXT IS FINISHED. PRESS A KEY TO LEAVE."
TASTIDAPREMERE$="[ESC][<][TAB][`]" : TASTOFUGA$=Chr$(27) : NOMEPROGRAMMA$="[TextReader]"
ARCOBALENO=0 : BANCOMUSICA=1023 : TIPOARCOBALENO=0 : SCHERMO=0 : TESTO=1
If Ntsc=True Then ALTEZZASCHERMO=200 Else ALTEZZASCHERMO=256
LUNGHEZZAARCOBALENO=ALTEZZASCHERMO+1 : LINEEDITESTO=ALTEZZASCHERMO/8
Screen Open SCHERMO,640,ALTEZZASCHERMO,2,Hires : Curs Off : Flash Off 
XSCHERMO=128 : YSCHERMO=42
Screen Display SCHERMO,XSCHERMO,YSCHERMO,640,ALTEZZASCHERMO
Palette $C0,$CCC : Track Loop On : Wait Vbl : Limit Mouse 
Repeat 
 Cls 
 NOMEEPERCORSODELTESTO$=Fsel$("**",NOMEDELTESTO$,NOMEPROGRAMMA$+" Please select a text to view...","Press the Esc key to leave.")
 If Exist(NOMEEPERCORSODELTESTO$)
  POSIZIONE=1
  Repeat 
   If PERCORSO=1
    PERCORSO=Instr(NOMEEPERCORSODELTESTO$,":",POSIZIONE) : POSIZIONE=PERCORSO+1
   End If 
   PERCORSO=Instr(NOMEEPERCORSODELTESTO$,"/",POSIZIONE)
   If PERCORSO>0
    POSIZIONE=PERCORSO+1
   End If 
  Until PERCORSO=0
  NOMEDELTESTO$=Right$(NOMEEPERCORSODELTESTO$,Len(NOMEEPERCORSODELTESTO$)-POSIZIONE+1)
  Open In TESTO,NOMEEPERCORSODELTESTO$
  Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"","","(1,1,1)"
  Rainbow ARCOBALENO,0,YSCHERMO,LUNGHEZZAARCOBALENO
  Locate 0,LINEEDITESTO-1
  Repeat 
   LINEA=1 : TESTOGIUNTOAFONDOSCHERMO=False
   Repeat 
    If POSIZIONEINTERROTTA=0
     LINEADITESTO$=Input$(TESTO,80) : POSIZIONE=1
    Else 
     POSIZIONE=POSIZIONEINTERROTTA : POSIZIONEINTERROTTA=0
    End If 
    Repeat 
     ACAPO=Instr(LINEADITESTO$,Chr$(10),POSIZIONE)
     If ACAPO>0
      LUNGHEZZALINEA=ACAPO-POSIZIONE
      TESTOACAPO$=Mid$(LINEADITESTO$,POSIZIONE,LUNGHEZZALINEA)
      LUNGHEZZALINEA=Len(ULTIMALINEA$)+Len(TESTOACAPO$)
      If LUNGHEZZALINEA>80
       TESTOACAPO$=Left$(TESTOACAPO$,80-Len(ULTIMALINEA$))
       Print TESTOACAPO$;
      Else If LUNGHEZZALINEA=80
       Print TESTOACAPO$;
      Else 
       Print TESTOACAPO$
      End If 
      If LINEAINCOMPLETAAFONDOSCHERMO=True
       Exit 
      Else 
       If LINEA=LINEEDITESTO-1
        LINEAINCOMPLETAAFONDOSCHERMO=True : Exit 
       End If 
      End If 
      POSIZIONE=ACAPO+1 : Inc LINEA : ULTIMALINEA$=TESTOACAPO$
     End If 
    Until ACAPO=0
    If LINEAINCOMPLETAAFONDOSCHERMO=True
     POSIZIONEINTERROTTA=POSIZIONE : LINEAINCOMPLETAAFONDOSCHERMO=False
    Else 
     LUNGHEZZALINEA=80-POSIZIONE+1 : ULTIMALINEAPRECEDENTE$=ULTIMALINEA$
     If LUNGHEZZALINEA>0
      ULTIMALINEA$=Right$(LINEADITESTO$,LUNGHEZZALINEA)
      If LUNGHEZZALINEA=80
       LUNGHEZZALINEAPRECEDENTE=Len(ULTIMALINEAPRECEDENTE$)
       If LUNGHEZZALINEAPRECEDENTE<80
        Print Left$(ULTIMALINEA$,80-Len(ULTIMALINEAPRECEDENTE$));
       End If 
      Else 
       Print ULTIMALINEA$;
      End If 
      If LINEA=LINEEDITESTO-1
       LINEAINCOMPLETAAFONDOSCHERMO=True
      End If 
     End If 
    End If 
    If LINEA=LINEEDITESTO-1 and LINEAINCOMPLETAAFONDOSCHERMO=False
     TESTOGIUNTOAFONDOSCHERMO=True
    End If 
   Until TESTOGIUNTOAFONDOSCHERMO=True or Eof(TESTO)=True
   Gosub MESSAGGIAVIDEOEDAZIONI
  Until Eof(TESTO)=True or FINEDELPROGRAMMA=True
  Close TESTO
  If FINEDELPROGRAMMA=True
   FINEDELPROGRAMMA=False
  End If 
 Else 
  FINEDELPROGRAMMA=True
 End If 
 Rainbow Del ARCOBALENO
Until FINEDELPROGRAMMA=True
Track Stop : Track Loop Of : Screen Close SCHERMO
End 
MESSAGGIAVIDEOEDAZIONI:
CAMBIOMESSAGGIO=1
Repeat 
 TASTOPREMUTO$=Inkey$
 If TASTOPREMUTO$=Tab$
  If MOVIMENTOARCOBALENO=0
   MOVIMENTOARCOBALENO=1
  Else If MOVIMENTOARCOBALENO=1
   MOVIMENTOARCOBALENO=-1
  Else 
   Rainbow ARCOBALENO,0,YSCHERMO,LUNGHEZZAARCOBALENO
   MOVIMENTOARCOBALENO=0
  End If 
  TASTOPREMUTO$=""
 End If 
 If TASTOPREMUTO$=TASTOFUGA$
  FINEDELPROGRAMMA=True
 End If 
 If TASTOPREMUTO$="`" or TASTOPREMUTO$="~"
  If SUONA=False
   Track Play : SUONA=True
  Else 
   Track Stop : SUONA=False
  End If 
  TASTOPREMUTO$=""
 End If 
 If TASTOPREMUTO$="<"
  Rainbow Del ARCOBALENO
  If C0LOREARCOBALENO=0
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"","(1,1,1)",""
   C0LOREARCOBALENO=1
  Else If C0LOREARCOBALENO=1
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"(1,1,1)","",""
   C0LOREARCOBALENO=2
  Else If C0LOREARCOBALENO=2
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"","(1,1,1)","(1,1,1)"
   C0LOREARCOBALENO=3
  Else If C0LOREARCOBALENO=3
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"(1,1,1)","(1,1,1)",""
   C0LOREARCOBALENO=4
  Else If C0LOREARCOBALENO=4
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"(1,1,1)","","(1,1,1)"
   C0LOREARCOBALENO=5
  Else If C0LOREARCOBALENO=5
   Set Rainbow ARCOBALENO,TIPOARCOBALENO,ALTEZZASCHERMO,"","","(1,1,1)"
   C0LOREARCOBALENO=0
  End If 
  Rainbow ARCOBALENO,0,YSCHERMO,LUNGHEZZAARCOBALENO : TASTOPREMUTO$=""
 End If 
 If MOVIMENTOARCOBALENO<>0
  Add POSIZIONEARCOBALENO,MOVIMENTOARCOBALENO,0 To 15
  Rainbow ARCOBALENO,POSIZIONEARCOBALENO,YSCHERMO,LUNGHEZZAARCOBALENO
 End If 
 If Y Screen(Y Mouse)>ALTEZZASCHERMO-9
  If CAMBIOMESSAGGIO=0
   Locate 0,LINEEDITESTO-1 : Inverse On : Cline 
   Print "TEXT>";NOMEDELTESTO$; : Inverse Off : CAMBIOMESSAGGIO=1
  End If 
 Else 
  If CAMBIOMESSAGGIO=1
   Locate 0,LINEEDITESTO-1 : Inverse On : Cline 
   If Eof(TESTO)=True
    Centre FINEDELTESTOPREMIUNTASTOPERUSCIRE$
   Else 
    Centre FINEDELLAPAGINAPREMIUNTASTOPERCONTINUARE$
   End If 
   Inverse Off : Ink 0,1
   Text(80-Len(TASTIDAPREMERE$))*8,LINEEDITESTO*8-2,TASTIDAPREMERE$
   CAMBIOMESSAGGIO=0
  End If 
 End If 
 Wait Vbl 
Until TASTOPREMUTO$<>""
Locate 0,LINEEDITESTO-1 : Cline 
Return
