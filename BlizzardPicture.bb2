;-> BlizzardPicture by Stefano Maria Regattin
;i> 2 gennaio 2011
;m> 15,29 gennaio 2011
;m> 25,26,27,28 febbraio 2011
;m> 3,23 marzo 2011
;m> 24 aprile 2011, Pasqua
;m> 4,5 agosto 2011
;m> 31 ottobre 2012
;m> 16 dicembre 2012
;m> 13,14,27 novembre 2013
;m> 6 dicembre 2013
;m> 26 marzo 2018
;-----------------

JMP InizioDelProgramma

Dc.b "$VER: BlizzardPicture 1.3 (26.3.2018)"

Even

InizioDelProgramma

;high resolution flag
#AltaRisoluzione=32768

;screen graphics area identifier
#AreaGraficaSchermo=0

;both mouse buttons click value
#EntrambiITastiDelTopoPremuti=3

;image shape identifier
#FormaImmagine=0

;topaz font size
#GrandezzaFonteTopaz=8

;screen identifier
#Schermo=0

;sound identifier
#Suono=0


;left mouse button click value
#TastoSinistroDelTopoPremuto=1

;right mouse button click value
#TastoDestroDelTopoPremuto=2

;image palette identifier
#TavolozzaDeiColori=0

;blacks palette identifier
#TavolozzaDeiNeri=1

NEWTYPE .Tags
 XPosTag.l:XPos.l
 YPosTag.l:YPos.l
 XLenTag.l:XLen.l
 YLenTag.l:YLen.l
 DeptTag.l:Dept.l
 DPenTag.l:DPen.l
 BPenTag.l:BPen.l
 ColsTag.l:Cols.l
 ModeTag.l:Mode.l
 IdenTag.l:Iden.l
End NEWTYPE

;image file path
PercorsoImmagine$="S:bLiZZARd_as.IFF"

;sound file path
PercorsoSuono$="S:ChillLoopUno.8svx"

;screen title
TitoloSchermo$=Centre$("BlizzardPicture 1.3 (26.3.2018)",80)

DEFTYPE .w ColoreBianco,ColoreNero,ColoreRosso,ColoriDellImmagine

Statement DissolvenzaDeiColori{DissolvenzaVersoIlNero.b}
SHARED ColoriDellImmagine
 If DissolvenzaVersoIlNero=False
  For Dissolvenza.q=0.01 To 1 Step 0.01
   FadePalette #TavolozzaDeiColori,#TavolozzaDeiNeri,Dissolvenza:Use Palette #TavolozzaDeiNeri:VWait
  Next Dissolvenza
 Else
  For Dissolvenza=1 To 0.01 Step -0.01
   FadePalette #TavolozzaDeiColori,#TavolozzaDeiNeri,Dissolvenza:Use Palette #TavolozzaDeiNeri:VWait
  Next Dissolvenza
  Free Palette #TavolozzaDeiNeri:Free Palette #TavolozzaDeiColori
 EndIf
End Statement

Statement TrovaIColori{}
SHARED ColoreBianco
SHARED ColoreNero
SHARED ColoreRosso
SHARED ColoriDellImmagine
 ColoreBianco=0:RossoBianco.w=0:VerdeBianco.w=0:BluBianco.w=0
 ColoreNero=1:RossoNero.w=255:VerdeNero.w=255:BluNero.w=255
 ColoreRosso=1:RossoRosso.w=0:VerdeRosso=255:BluRosso=255
 For Colore.w=0 To ColoriDellImmagine-1
  Rosso.w=AGARed(Colore):Verde.w=AGAGreen(Colore):Blu.w=AGABlue(Colore)
  If Rosso>RossoBianco AND Verde>VerdeBianco AND Blu>BluBianco
   ColoreBianco=Colore:RossoBianco=Rosso:VerdeBianco=Verde:BluBianco=Blu
  EndIf
  If Rosso<RossoNero AND Verde<VerdeNero AND Blu<BluNero
   ColoreNero=Colore:RossoNero=Colore:VerdeNero=Verde:BluNero=Blu
  EndIf
  If Rosso>=RossoRosso AND Verde<=VerdeRosso AND Blu<=BluRosso
   ColoreRosso=Colore:RossoRosso=Rosso:VerdeRosso=Verde:BluRosso=Blu
  EndIf
 Next Colore
End Function

If Exists(PercorsoImmagine$)>0
 ILBMInfo PercorsoImmagine$
 AltezzaImmagine.w=ILBMHeight:LarghezzaImmagine.w=ILBMWidth
 ProfonditaImmagine=ILBMDepth:ColoriDellImmagine=2^ProfonditaImmagine
 InitPalette #TavolozzaDeiColori,ColoriDellImmagine
 LoadShape #FormaImmagine,PercorsoImmagine$,#TavolozzaDeiColori
 InitPalette #TavolozzaDeiNeri,ColoriDellImmagine
EndIf

Schermo.Tags\XPosTag=$80000021,0
Schermo.Tags\YPosTag=$80000022,0
Schermo.Tags\XLenTag=$80000023,640
Schermo.Tags\YLenTag=$80000024,256
Schermo.Tags\DeptTag=$80000025,ProfonditaImmagine
Schermo.Tags\DPenTag=$80000026,ColoreNero
Schermo.Tags\BPenTag=$80000027,ColoreBianco
Schermo.Tags\ColsTag=$80000028,ColoriDellImmagine
Schermo.Tags\ModeTag=$8000002D,#AltaRisoluzione
Schermo.Tags\IdenTag=$80000032,#AltaRisoluzione
ScreenTags #Schermo,TitoloSchermo$,Schermo
Use Screen #Schermo:Use Palette #TavolozzaDeiNeri
ScreensBitMap #Schermo,#AreaGraficaSchermo
BitMapOutput #AreaGraficaSchermo

TrovaIColori{}

If Exists(PercorsoImmagine$)>0
 XPosFormaImmagine.w=(ScreenWidth-LarghezzaImmagine)/2:YPosFormaImmagine.w=(ScreenHeight-AltezzaImmagine)/2
 Boxf 0,0,ScreenWidth-1,ScreenHeight-1,ColoreBianco
 Blit #FormaImmagine,XPosFormaImmagine,YPosFormaImmagine:Free Shape #FormaImmagine
EndIf

DissolvenzaDeiColori{False}

;inizio parte sonora-sonic part begin
If Exists(PercorsoSuono$)>0
 LoadSound #Suono,PercorsoSuono$
 LoopSound #Suono,3,0,0
 SuonoPresente.b=True
 For IntensitaSuono.b=1 To 64
  Volume 3,IntensitaSuono,IntensitaSuono:VWait
 Next IntensitaSuono
EndIf
;fine parte sonora sonora-sonic part end

Colour ColoreNero,ColoreBianco
Locate 0,31:Print "Tasto sinistro del mouse->lancio Magellan - Tasto destro del mouse->lancio OS3.9"
TastoDelTopoPremuto.b=0
Repeat
 TastoDelTopoPremuto=Joyb(0):If TastoDelTopoPremuto=0 Then TastoDelTopoPremuto=Joyb(1)
 VWait
Until TastoDelTopoPremuto>0

Locate 0,31:Colour ColoreRosso
If TastoDelTopoPremuto=#TastoSinistroDelTopoPremuto
 Print Centre$("Scelto Magellan - lancio Magellan attivato...",80)
EndIf
If TastoDelTopoPremuto=#TastoDestroDelTopoPremuto
 Print Centre$("Scelto OS 3.9 - lancio OS 3.9 attivato...",80)
EndIf

;inizio parte sonora-sonic part begin
If SuonoPresente=True
 For IntensitaSuono=64 To 0 Step -1
  Volume 3,IntensitaSuono,IntensitaSuono:VWait
 Next IntensitaSuono
 Quiet 3:Free Sound #Suono
EndIf
;fine parte sonora-sonic part end

DissolvenzaDeiColori{True}
CloseScreen #Schermo

If TastoDelTopoPremuto=#TastoSinistroDelTopoPremuto
 Exec "DOpus5:C/LoadDB"
EndIf

If TastoDelTopoPremuto=#TastoDestroDelTopoPremuto
 Exec "C:LoadWB"
 Run "SYS:OutlineFont/OutlineFont","2 1 2 SHADOW"
 Run "SYS:WBStartup/OS3.9Only/DefIcons",""
 Run "SYS:WBStartup/OS3.9Only/RAWBInfo",""
 Run "SYS:WBStartup/OS3.9Only/MattoniteBoing",""
EndIf
End
