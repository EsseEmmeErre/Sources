;-> Alba English by Stefano Maria Regattin
;d> 12 settembre 1999 da Alba2FNS
;m> 5,8,28 ottobre 1999
;m> 23 novembre 1999
;m> 20,29 dicembre 1999
;m> 25 febbraio 2000
;m> 9 marzo 2000
;m> 20,25 gennaio 2001
;m> 12,14,15 febbraio 2001
;m> 11,15,19,20 luglio 2001
;m> 5,22 agosto 2001
;m> 30 settembre 2001
;m> 10 dicembre 2001
;m> 7 settembre 2002
;m> 1 marzo 2003
;m> 13 agosto 2003
;m> 19,20,21,22 gennaio 2005
;m> 8 ottobre 2011
;------------------

;----------------------------------------------------------------------
;le seguenti linee servono per la corretta gestione della versione del
; programma
;the following lines are for the correct handling of the program's version
;--------------------------------------------------------------------------
JMP InizioDelProgramma

;-----------------------
;versione del programma
;program's version
;------------------
Dc.b "$VER: AlbaEng 3.6 (8.10.2011)"

Even

InizioDelProgramma

#AreaGrafica=0
#CanaleFile=0
#ColoreSfondo=0
#ColoreTesto=1
#FineLinea=10
#GraficaAGA=65536
#Fonte=0
#InizioSchermo=44
#LarghezzaSchermo=320
#ListaCopper=0
#ProfonditaSchermo=1
#TavolozzaColori=0

Aiuto.b=False
ChiediLaNuovaParolaChiave.b=False
ControllaLaData.b=False
FileAperto.b=False
SchermoAGA.b=False
ScriviLaParolaChiave.b=False

DEFTYPE .w AltezzaSchermo

NEWTYPE .StrutturaColori
 RossoAlto.l
 VerdeAlto.l
 BluAlto.l
 RossoBasso.l
 VerdeBasso.l
 BluBasso.l
End NEWTYPE
DEFTYPE .StrutturaColori Colori

NEWTYPE .RagioneDelColore
 Rosso.q
 Verde.q
 Blu.q
End NEWTYPE
DEFTYPE .RagioneDelColore RagioneDelColoreAlto,RagioneDelColoreBasso,RagioneDelColoreTesto

NEWTYPE .ColoreIntero
 Rosso.w
 Verde.w
 Blu.w
End NEWTYPE
DEFTYPE .ColoreIntero ColoreAlto,ColoreBasso,ColoreTesto

Function ConvertiLaCifraDaEsadecimaleADecimale{Cifra$}
 Select Cifra$
 Case "0"
  Cifra=0
 Case "1"
  Cifra=1
 Case "2"
  Cifra=2
 Case "3"
  Cifra=3
 Case "4"
  Cifra=4
 Case "5"
  Cifra=5
 Case "6"
  Cifra=6
 Case "7"
  Cifra=7
 Case "8"
  Cifra=8
 Case "9"
  Cifra=9
 Case "A"
  Cifra=10
 Case "B"
  Cifra=11
 Case "C"
  Cifra=12
 Case "D"
  Cifra=13
 Case "E"
  Cifra=14
 Case "F"
  Cifra=15
 End Select
Function Return Cifra
End Function

Statement SpiegaLUsoDelComando{}
 NPrint "ASKKEYWORDCHANGE/S DATECHECK/S[=DateEd]"
 NPrint "where DateEd is SYS:Prefs/Time if not specified"
 NPrint "TOPCOLOUR/K COLOUR BOTTOMCOLOUR/K COLOUR TEXTCOLOUR/K COLOUR"
 NPrint "where COLOUR can be [[RR]GG]BB/N or RANDOM/S"
 NPrint "AGASCREEN/S to use all the colours available on AGA Amigas"
 NPrint "***********************************************************"
 NPrint "ASKKEYWORDCHANGE/S (ChiediSeSiVuoleCambiareLaParolaChiave)"
 NPrint "DATECHECK/S[=EditoreData] (ControllaLaData)"
 NPrint "dove EditoreData e' SYS:Prefs/Time se non specificato"
 NPrint "TOPCOLOUR/K (ColoreAlto) COLORE"
 NPrint "BOTTOMCOLOUR/K (ColoreBasso) COLORE"
 NPrint "TEXTCOLOUR/K (ColoreTesto) COLORE"
 NPrint "dove COLORE puo' essere [[RR]VV]BB/N (un numero esadecimale)"
 NPrint "oppure puo' essere RANDOM/S (casuale)"
 NPrint "AGASCREEN/S (SchermoAGA) per usare tutti i colori presenti"
 NPrint "sugli Amiga AGA"
End Statement

Function .b LeggiParametri{}
SHARED ChiediLaNuovaParolaChiave
SHARED ColoreAlto
SHARED ColoreBasso
SHARED ColoreTesto
SHARED ControllaLaData
SHARED EditoreData$
SHARED SchermoAGA
 Dim Parametro$(8)
 ParametriGiusti.b=True
 If NumPars>9
  SpiegaLUsoDelComando{}
  ParametriGiusti=False
 Else
  If NumPars>0
   For Parametro.b=1 To NumPars
    Parametro$(Parametro-1)=UCase$(Par$(Parametro))
   Next Parametro
   For Parametro=1 To NumPars
    Parametro$=UCase$(Parametro$(Parametro-1))
    If Left$(Parametro$,Len("DATECHECK"))="DATECHECK"
     EditoreData$=UnLeft$(Parametro$,Len("DATECHECK="))
     Parametro$=Left$(Parametro$,Len("DATECHECK"))
    EndIf
    Select Parametro$
    Case "ASKKEYWORDCHANGE"
     ChiediLaNuovaParolaChiave=True
    Case "DATECHECK"
     ControllaLaData=True
    Case "TOPCOLOUR"
     Colore$=Parametro$(Parametro)
     If Colore$="RANDOM"
      ColoreAlto\Rosso=Rnd(255)
      ColoreAlto\Verde=Rnd(255)
      ColoreAlto\Blu=Rnd(255)
     Else
      CifreEsadecimali.b=Len(Colore$)
      For CifraEsadecimale.b=0 To CifreEsadecimali-1
       Colore.b=ConvertiLaCifraDaEsadecimaleADecimale{Mid$(Colore$,CifreEsadecimali-CifraEsadecimale,1)}
       Select CifraEsadecimale
       Case 0
        ColoreAlto\Blu=Colore
       Case 1
        ColoreAlto\Blu+Colore*16
       Case 2
        ColoreAlto\Verde=Colore
       Case 3
        ColoreAlto\Verde+Colore*16
       Case 4
        ColoreAlto\Rosso=Colore
       Case 5
        ColoreAlto\Rosso+Colore*16
       Default
        SpiegaLUsoDelComando{}
        ParametriGiusti=False
       End Select
      Next CifraEsadecimale
     EndIf
     ColoreTrovato=True
    Case "BOTTOMCOLOUR"
     Colore$=Parametro$(Parametro)
     If Colore$="RANDOM"
      ColoreBasso\Rosso=Rnd(255)
      ColoreBasso\Verde=Rnd(255)
      ColoreBasso\Blu=Rnd(255)
     Else
      CifreEsadecimali=Len(Colore$)
      For CifraEsadecimale=0 To CifreEsadecimali-1
       Colore=ConvertiLaCifraDaEsadecimaleADecimale{Mid$(Colore$,CifreEsadecimali-CifraEsadecimale,1)}
       Select CifraEsadecimale
       Case 0
        ColoreBasso\Blu=Colore
       Case 1
        ColoreBasso\Blu+Colore*16
       Case 2
        ColoreBasso\Verde=Colore
       Case 3
        ColoreBasso\Verde+Colore*16
       Case 4
        ColoreBasso\Rosso=Colore
       Case 5
        ColoreBasso\Rosso+Colore*16
       Default
        SpiegaLUsoDelComando{}
        ParametriGiusti=False
       End Select
      Next CifraEsadecimale
     EndIf
     ColoreTrovato=True
    Case "TEXTCOLOUR"
     Colore$=Parametro$(Parametro)
     If Colore$="RANDOM"
      ColoreTesto\Rosso=Rnd(255)
      ColoreTesto\Verde=Rnd(255)
      ColoreTesto\Blu=Rnd(255)
     Else
      CifreEsadecimali=Len(Colore$)
      For CifraEsadecimale=0 To CifreEsadecimali-1
       Colore=ConvertiLaCifraDaEsadecimaleADecimale{Mid$(Colore$,CifreEsadecimali-CifraEsadecimale,1)}
       Select CifraEsadecimale
       Case 0
        ColoreTesto\Blu=Colore
       Case 1
        ColoreTesto\Blu+Colore*16
       Case 2
        ColoreTesto\Verde=Colore
       Case 3
        ColoreTesto\Verde+Colore*16
       Case 4
        ColoreTesto\Rosso=Colore
       Case 5
        ColoreTesto\Rosso+Colore*16
       Default
        SpiegaLUsoDelComando{}
        ParametriGiusti=False
       End Select
      Next CifraEsadecimale
     EndIf
     ColoreTrovato=True
    Case "AGASCREEN"
     SchermoAGA=True
    Default
     If ColoreTrovato=False
      SpiegaLUsoDelComando{}
      ParametriGiusti=False
     EndIf
    End Select
   Next Parametro
  EndIf
 EndIf
 Function Return ParametriGiusti
End Function

Statement InvertiIlColoreTestoSeITreColoriSonoUguali{}
SHARED ColoreAlto
SHARED ColoreBasso
SHARED ColoreTesto
 If ColoreAlto\Rosso=ColoreTesto\Rosso AND ColoreBasso\Rosso=ColoreTesto\Rosso
  If ColoreAlto\Verde=ColoreTesto\Verde AND ColoreBasso\Verde=ColoreTesto\Verde
   If ColoreAlto\Blu=ColoreTesto\Blu AND ColoreBasso\Blu=ColoreTesto\Blu
    ColoreTesto\Rosso=$FF-ColoreTesto\Rosso
    ColoreTesto\Verde=$FF-ColoreTesto\Verde
    ColoreTesto\Blu=$FF-ColoreTesto\Blu
   EndIf
  EndIf
 EndIf
End Statement

Statement CalcolaLeRagioniDeiColori{}
SHARED ColoreAlto
SHARED ColoreBasso
SHARED ColoreTesto
SHARED RagioneDelColoreAlto
SHARED RagioneDelColoreBasso
SHARED RagioneDelColoreTesto
 RagioneDelColoreAlto\Rosso=ColoreAlto\Rosso/(255*1.0)
 RagioneDelColoreAlto\Verde=ColoreAlto\Verde/(255*1.0)
 RagioneDelColoreAlto\Blu=ColoreAlto\Blu/(255*1.0)
 RagioneDelColoreBasso\Rosso=ColoreBasso\Rosso/(255*1.0)
 RagioneDelColoreBasso\Verde=ColoreBasso\Verde/(255*1.0)
 RagioneDelColoreBasso\Blu=ColoreBasso\Blu/(255*1.0)
 RagioneDelColoreTesto\Rosso=ColoreTesto\Rosso/(255*1.0)
 RagioneDelColoreTesto\Verde=ColoreTesto\Verde/(255*1.0)
 RagioneDelColoreTesto\Blu=ColoreTesto\Blu/(255*1.0)
End Statement

Statement LeggiLaParolaChiave{}
SHARED ParolaChiave$
SHARED FileAperto
 FileAperto=ReadFile(#CanaleFile,"S:Password")
 If FileAperto=True
  FileInput #CanaleFile
  ParolaChiave$=Edit$(9)
  CloseFile #CanaleFile
  PopInput
  ParolaChiave$=StripTrail$(ParolaChiave$,#FineLinea)
 EndIf
End Statement

Statement LeggiLaDataDellUltimoBoot{}
SHARED DataUltimoBoot$
SHARED FileAperto
 FileAperto=ReadFile(#CanaleFile,"S:LastBootDate")
 If FileAperto=True
  FileInput #CanaleFile
  DataUltimoBoot$=Edit$(10)
  CloseFile #CanaleFile
  PopInput
 EndIf
End Statement

Statement ScorrimentoTesto{Modo.b}
SHARED Qualificatore$
SHARED Stringa$
 If Modo=True
  LunghezzaTesto.w=FNSLength(#Fonte,Qualificatore$)+2
  For Scorrimento=#LarghezzaSchermo-1 To #LarghezzaSchermo-1-LunghezzaTesto Step -1
   FNSInk #ColoreTesto
   FNSPrint #Fonte,Scorrimento,0,Qualificatore$
   Delay_ 1
   If Scorrimento=#LarghezzaSchermo-1-LunghezzaTesto Then Statement Return
   FNSInk #ColoreSfondo
   FNSPrint #Fonte,Scorrimento,0,Qualificatore$
  Next Scorrimento
 Else
  LunghezzaTesto=FNSLength(#Fonte,Stringa$)+2
  For Scorrimento=1 To LunghezzaTesto
   FNSInk #ColoreTesto
   FNSPrint #Fonte,Scorrimento-LunghezzaTesto,0,Stringa$
   Delay_ 1
   If Scorrimento=LunghezzaTesto Then Statement Return
   FNSInk #ColoreSfondo
   FNSPrint #Fonte,Scorrimento-LunghezzaTesto,0,Stringa$
  Next Scorrimento
 EndIf
End Statement

Statement ScorrimentoInversoTesto{Modo.b}
SHARED Qualificatore$
SHARED Stringa$
 If Modo=True
  LunghezzaTesto=FNSLength(#Fonte,Qualificatore$)+2
  For Scorrimento=#LarghezzaSchermo-1-LunghezzaTesto To #LarghezzaSchermo-1
   FNSInk #ColoreTesto
   FNSPrint #Fonte,Scorrimento,0,Qualificatore$
   Delay_ 1
   FNSInk #ColoreSfondo
   FNSPrint #Fonte,Scorrimento,0,Qualificatore$
  Next Scorrimento
 Else
  LunghezzaTesto=FNSLength(#Fonte,Stringa$)+2
  For Scorrimento=LunghezzaTesto To 1 Step -1
   FNSInk #ColoreTesto
   FNSPrint #Fonte,Scorrimento-LunghezzaTesto,0,Stringa$
   Delay_ 1
   FNSInk #ColoreSfondo
   FNSPrint #Fonte,Scorrimento-LunghezzaTesto,0,Stringa$
  Next Scorrimento
 EndIf
 FNSInk #ColoreTesto
End Statement

Statement LeggiITasti{}
SHARED Qualificatore$
SHARED Aiuto
SHARED ParolaInserita$
SHARED Stringa$
SHARED X
 ParolaInserita$=""
 X+2
 Repeat
  Repeat
   TastoPremuto$=Inkey$
   CapsLock.b=RawStatus($62)
   Help.b=RawStatus($5F)
   If CapsLock=True AND Help=True Then Aiuto=True:Statement Return
   Delay_ 1
  Until TastoPremuto$<>""
  If Asc(TastoPremuto$)>31
   ParolaInserita$+TastoPremuto$:FNSPrint 0,X,0,"*"
   Stringa$+"*"
   X+FNSLength(#Fonte,"*")+2
   If Len(ParolaInserita$)>7 Then Statement Return
  Else
   Select TastoPremuto$
   Case Chr$(8)
    If ParolaInserita$<>""
     ParolaInserita$=UnLeft$(ParolaInserita$,1)
     Stringa$=UnLeft$(Stringa$,1)
     X-(FNSLength(#Fonte,"*")+2)
     FNSInk #ColoreSfondo:FNSPrint 0,X,0,"*":FNSInk #ColoreTesto
    EndIf
   Case Chr$(13)
    If ParolaInserita$<>"" Then Statement Return
   Default
    Qualificatore=BlitzQualifier
    If Qualificatore>0 AND Qualificatore<$100
     Select Qualificatore
     Case 1
      Qualificatore$="LShift"
      ScorrimentoTesto{True}
     Case 2
      Qualificatore$="RShift"
      ScorrimentoTesto{True}
     Case 4
      Qualificatore$="Caps"
      ScorrimentoTesto{True}
     Case 8
      Qualificatore$="Ctrl"
      ScorrimentoTesto{True}
     Case $10
      Qualificatore$="LAlt"
      ScorrimentoTesto{True}
     Case $20
      Qualificatore$="RAlt"
      ScorrimentoTesto{True}
     Case $40
      Qualificatore$="LAmiga"
      ScorrimentoTesto{True}
     Case $80
      Qualificatore$="RAmiga"
      ScorrimentoTesto{True}
     Default
      Qualificatore$="2Or+"
      ScorrimentoTesto{True}
     End Select
     Delay_ 100
     ScorrimentoInversoTesto{True}
     Help=RawStatus($5F)
     If Qualificatore=4 AND Help=True Then Aiuto=True:Statement Return
    EndIf
   End Select
  EndIf
 Until False
End Statement

Statement ScriviLaParolaChiave{}
 SHARED ParolaChiave$
 FileAperto=WriteFile(#CanaleFile,"S:Keyword")
 If FileAperto=True
  FileOutput #CanaleFile
  NPrint ParolaChiave$
  CloseFile #CanaleFile
  PopOutput
 EndIf
End Statement

Statement ScriviLaDataDiOggi{}
 SHARED DataUltimoBoot$
 FileAperto=WriteFile(#CanaleFile,"S:LastBootDate")
 If FileAperto=True
  FileOutput #CanaleFile
  Print DataUltimoBoot$
  CloseFile #CanaleFile
  PopOutput
 EndIf
End Statement

Statement VecchioArcobaleno{}
SHARED AltezzaSchermo
SHARED ColoreAlto
SHARED ColoreBasso
 For PorzioneSchermo.b=1 To AltezzaSchermo/8
  If ColoreAlto\Rosso=ColoreBasso\Rosso
   RossoPorzione.b=ColoreAlto\Rosso/16
  Else
   If ColoreAlto\Rosso>ColoreBasso\Rosso
    RossoPorzione=(ColoreAlto\Rosso-ColoreBasso\Rosso)/16*(AltezzaSchermo-PorzioneSchermo*8)/AltezzaSchermo
   Else
    RossoPorzione=(ColoreBasso\Rosso-ColoreAlto\Rosso)/16*PorzioneSchermo*8/AltezzaSchermo
   EndIf
  EndIf
  If ColoreAlto\Verde=ColoreBasso\Verde
   VerdePorzione.b=ColoreAlto\Verde/16
  Else
   If ColoreAlto\Verde>ColoreBasso\Verde
    VerdePorzione=(ColoreAlto\Verde-ColoreBasso\Verde)/16*(AltezzaSchermo-PorzioneSchermo*8)/AltezzaSchermo
   Else
    VerdePorzione=(ColoreBasso\Verde-ColoreAlto\Verde)/16*PorzioneSchermo*8/AltezzaSchermo
   EndIf
  EndIf
  If ColoreAlto\Blu=ColoreBasso\Blu
   BluPorzione.b=ColoreAlto\Blu/16
  Else
   If ColoreAlto\Blu>ColoreBasso\Blu
    BluPorzione=(ColoreAlto\Blu-ColoreBasso\Blu)/16*(AltezzaSchermo-PorzioneSchermo*8)/AltezzaSchermo
   Else
    BluPorzione=(ColoreBasso\Blu-ColoreAlto\Blu)/16*PorzioneSchermo*8/AltezzaSchermo
   EndIf
  EndIf
  DisplayRGB #ListaCopper,#ColoreSfondo,(PorzioneSchermo-1)*8,RossoPorzione,VerdePorzione,BluPorzione
 Next PorzioneSchermo
End Statement

;************************************************
;* Fine delle procedure ed inizio del programma *
;******* Procedures end and program start *******
;************************************************

WBStartup
If LeggiParametri{}=False Then Goto FineDelProgramma
InvertiIlColoreTestoSeITreColoriSonoUguali{}
CalcolaLeRagioniDeiColori{}
InitPalette #TavolozzaColori,2
AltezzaSchermo=DispHeight

If SchermoAGA=True
 InitCopList #ListaCopper,#InizioSchermo,AltezzaSchermo,#GraficaAGA+#ProfonditaSchermo,8,2,AltezzaSchermo*5+20
Else
 InitCopList #ListaCopper,#InizioSchermo,AltezzaSchermo,#ProfonditaSchermo,8,2,-32
EndIf

DisplayPalette #ListaCopper,#TavolozzaColori
BitMap #AreaGrafica,#LarghezzaSchermo,AltezzaSchermo,#ProfonditaSchermo
DisplayBitMap #ListaCopper,#AreaGrafica

If SchermoAGA=True
 Colori\RossoAlto=0,0,0:Colori\RossoBasso=0,0,0
 CopperReset #ListaCopper,#InizioSchermo
 DoColSplit &Colori,AltezzaSchermo,#ColoreSfondo
 CopperEnd
EndIf

FNSOrigin 0,AltezzaSchermo-8
BLITZ
CreateDisplay #ListaCopper

For Alba.w=0 To 255
 ColoreAlto\Rosso=RagioneDelColoreAlto\Rosso*Alba
 ColoreAlto\Verde=RagioneDelColoreAlto\Verde*Alba
 ColoreAlto\Blu=RagioneDelColoreAlto\Blu*Alba
 ColoreBasso\Rosso=RagioneDelColoreBasso\Rosso*Alba
 ColoreBasso\Verde=RagioneDelColoreBasso\Verde*Alba
 ColoreBasso\Blu=RagioneDelColoreBasso\Blu*Alba
 Colori\RossoAlto=ColoreAlto\Rosso,ColoreAlto\Verde,ColoreAlto\Blu
 Colori\RossoBasso=ColoreBasso\Rosso,ColoreBasso\Verde,ColoreBasso\Blu
 If SchermoAGA=True
  RedoColSplit &Colori,AltezzaSchermo,#ColoreSfondo
 Else
  VecchioArcobaleno{}
 EndIf
 Rosso=RagioneDelColoreTesto\Rosso*1.0
 Verde=RagioneDelColoreTesto\Verde*1.0
 Blu=RagioneDelColoreTesto\Blu*1.0
 If SchermoAGA=True
  AGAPalRGB #TavolozzaColori,#ColoreTesto,Rosso*Alba,Verde*Alba,Blu*Alba
 Else
  PalRGB #TavolozzaColori,#ColoreTesto,Rosso*Alba/16,Verde*Alba/16,Blu*Alba/16
 EndIf
 DisplayPalette #ListaCopper,#TavolozzaColori
 VWait
Next Alba

BitMapOutput #AreaGrafica
BitMapInput
BlitzKeys On
FNSOutput #AreaGrafica,On
LeggiLaParolaChiave{}

If FileAperto=True
 Repeat
  Stringa$="Type the keyword>"
  ScorrimentoTesto{False}
  X=FNSLength(#Fonte,Stringa$)
  LeggiITasti{}
  ScorrimentoInversoTesto{False}
  If Aiuto=True Then Pop Repeat:Goto Uscita
  If ParolaInserita$<>ParolaChiave$
   Stringa$="The word does not match"
   ScorrimentoTesto{False}
   Delay_ 100
   ScorrimentoInversoTesto{False}
  Else
   If ChiediLaNuovaParolaChiave=True
    Stringa$="Do you want to change the keyword? (y/n)"
    ScorrimentoTesto{False}
    Repeat
     TastoPremuto$=Inkey$
     VWait
    Until TastoPremuto$<>""
    TastoPremuto$=LCase$(TastoPremuto$)
    If TastoPremuto$="y"
     ScorrimentoInversoTesto{False}
     Stringa$="Type the new keyword>"
     ScorrimentoTesto{False}
     X=FNSLength(#Fonte,Stringa$)
     LeggiITasti{}
     ScorrimentoInversoTesto{False}
     ParolaChiave$=ParolaInserita$
     ScriviLaParolaChiave=True
    Else
     ScorrimentoInversoTesto{False}
    EndIf
   EndIf
  EndIf
 Until ParolaInserita$=ParolaChiave$
Else
 Stringa$="Type the new keyword>"
 ScorrimentoTesto{False}
 X=FNSLength(#Fonte,Stringa$)
 LeggiITasti{}
 ScorrimentoInversoTesto{False}
 If Aiuto=True Then Pop If:Goto Uscita
 ParolaChiave$=ParolaInserita$
 ScriviLaParolaChiave=True
EndIf
If ScriviLaParolaChiave=True Then ScriviLaParolaChiave{}

Uscita

For Alba=255 To 0 Step -1
 ColoreAlto\Rosso=RagioneDelColoreAlto\Rosso*Alba
 ColoreAlto\Verde=RagioneDelColoreAlto\Verde*Alba
 ColoreAlto\Blu=RagioneDelColoreAlto\Blu*Alba
 ColoreBasso\Rosso=RagioneDelColoreBasso\Rosso*Alba
 ColoreBasso\Verde=RagioneDelColoreBasso\Verde*Alba
 ColoreBasso\Blu=RagioneDelColoreBasso\Blu*Alba
 Colori\RossoAlto=ColoreAlto\Rosso,ColoreAlto\Verde,ColoreAlto\Blu
 Colori\RossoBasso=ColoreBasso\Rosso,ColoreBasso\Verde,ColoreBasso\Blu
 If SchermoAGA=True
  RedoColSplit &Colori,AltezzaSchermo,#ColoreSfondo
 Else
  VecchioArcobaleno{}
 EndIf
 Rosso=RagioneDelColoreTesto\Rosso*1.0
 Verde=RagioneDelColoreTesto\Verde*1.0
 Blu=RagioneDelColoreTesto\Blu*1.0
 If SchermoAGA=True
  AGAPalRGB #TavolozzaColori,#ColoreTesto,Rosso*Alba,Verde*Alba,Blu*Alba
 Else
  PalRGB #TavolozzaColori,#ColoreTesto,Rosso*Alba/16,Verde*Alba/16,Blu*Alba/16
 EndIf
 DisplayPalette #ListaCopper,#TavolozzaColori
 VWait
Next Alba

AMIGA

If ControllaLaData=True
 LeggiLaDataDellUltimoBoot{}
 If FileAperto=True
  DataDiOggi.l=SystemDate
  DataUltimoBoot.l=NumDays(DataUltimoBoot$)
  DifferenzaDate.l=DataDiOggi-DataUltimoBoot
  If Abs(DifferenzaDate)>1
   If EditoreData$="" Then EditoreData$="SYS:Prefs/Time"
   EseguitoEditoreData.b=Execute_(EditoreData$,0,0)
   If EseguitoEditoreData=False
    EZRequest "AlbaEng request",EditoreData$+" not found!","OK"
   EndIf
  EndIf
 EndIf
 DataUltimoBoot$=Date$(SystemDate)
 ScriviLaDataDiOggi{}
EndIf
FineDelProgramma
End
